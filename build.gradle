plugins {
  id 'java'
  id 'application'
  id 'jacoco'
}

import org.gradle.api.tasks.testing.logging.TestLogEvent

wrapper {
  gradleVersion = '6.0.1'
  distributionType = Wrapper.DistributionType.ALL
}

java {
  sourceCompatibility = JavaVersion.VERSION_13
  targetCompatibility = JavaVersion.VERSION_13
}

test {
  useTestNG()
  testLogging {
    events TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation "org.apache.commons:commons-lang3:3.9"
  implementation "org.slf4j:slf4j-api:1.7.29"
  implementation "com.google.guava:guava:28.1-jre"
  implementation "com.squareup.okhttp3:okhttp:4.2.2"

  runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.12.1"

  testImplementation "com.squareup.okhttp3:mockwebserver:4.2.2"
  testImplementation "org.mockito:mockito-core:3.1.0"
  testImplementation "org.testng:testng:7.0.0"
}


application {
  mainClassName = "net.akaritakai.aoc2019.Main"
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport